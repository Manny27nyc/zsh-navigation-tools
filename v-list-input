emulate -L zsh

zmodload zsh/curses

# Variables from this file are still in the scope
_vlist_check_if_previous_page() {
    if [ "$current" -lt "$from_what_idx_list_is_shown" ]; then
        from_what_idx_list_is_shown=from_what_idx_list_is_shown-page_height 
        [ "$from_what_idx_list_is_shown" -lt 1 ] && from_what_idx_list_is_shown=1
    fi
}

# Variables from this file are still in the scope
# The last_shown_idx_of_the_list variable is local to this scope
_vlist_check_if_next_page() {
    integer last_shown_idx_of_the_list=from_what_idx_list_is_shown+page_height-1
    if [ "$last_shown_idx_of_the_list" -ge "$last_element" ]; then
        # Next page is not possible
        return
    fi

    # Next page?
    if [ "$current" -gt "$last_shown_idx_of_the_list" ]; then
        from_what_idx_list_is_shown=from_what_idx_list_is_shown+page_height 
        # Defensive code - if current > last_shown_idx_of_the_list,
        # then (only by that argument) elements for next page should
        # be available (at least one)
        if [ "$from_what_idx_list_is_shown" -gt "$last_element" ]; then
            from_what_idx_list_is_shown=last_element
        fi
    fi
}

typeset -ga reply
reply=( -1 '' )
local start_idx_var="$1"
local current_idx_var="$2"
integer last_element="$3"

eval "integer from_what_idx_list_is_shown=\"$start_idx_var\""
eval "integer current=\"$current_idx_var\""

#
# Listening for input
#

case "$4" in
    (UP|k)
        # Are there any elements before the current one?
        [ "$current" -gt 1 ] && current=current-1;
        # Previous page?
        _vlist_check_if_previous_page
        ;;
    (DOWN|j)
        # Are there any elements after the current one?
        [ "$current" -lt "$last_element" ] && current=current+1;
        # Next page?
        _vlist_check_if_next_page
        ;;
    (PPAGE)
        current=current-page_height
        [ "$current" -lt 1 ] && current=1;
        _vlist_check_if_previous_page
        ;;
    (NPAGE|" ")
        current=current+page_height
        [ "$current" -gt "$last_element" ] && current=last_element;
        _vlist_check_if_next_page
        ;;
    (HOME|g)
        current=1
        from_what_idx_list_is_shown=1
        ;;
    (END|G)
        current=last_element
        from_what_idx_list_is_shown=last_element-page_height+1
        [ "$from_what_idx_list_is_shown" -lt 1 ] && from_what_idx_list_is_shown=1;
        ;;
    ($'\n')
        # Is that element selectable?
        if [[ ${VLIST_NONSELECTABLE_ELEMENTS[(r)$current]} != $current ]]; then
            # Save current element in the result variable
            reply=( $current SELECT )
        fi
        ;;
    (q)
            reply=( -1 QUIT )
        ;;
    (*)
        ;;
esac

eval "$start_idx_var=\"$from_what_idx_list_is_shown\""
eval "$current_idx_var=\"$current\""

# vim: set filetype=zsh:
