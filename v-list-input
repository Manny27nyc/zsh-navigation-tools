emulate -L zsh

zmodload zsh/curses

setopt localoptions
setopt typesetsilent

# Variables from this file are still in the scope
_vlist_check_if_previous_page() {
    if [ "$current_idx" -lt "$from_what_idx_list_is_shown" ]; then
        from_what_idx_list_is_shown=from_what_idx_list_is_shown-page_height 
        [ "$from_what_idx_list_is_shown" -lt 1 ] && from_what_idx_list_is_shown=1
    fi
}

# Variables from this file are still in the scope
# The last_shown_idx_of_the_list variable is local to this scope
_vlist_check_if_next_page() {
    integer last_shown_idx_of_the_list=from_what_idx_list_is_shown+page_height-1
    if [ "$last_shown_idx_of_the_list" -ge "$last_element" ]; then
        # Next page is not possible
        return
    fi

    # Next page?
    if [ "$current_idx" -gt "$last_shown_idx_of_the_list" ]; then
        from_what_idx_list_is_shown=from_what_idx_list_is_shown+page_height 
        # Defensive code - if current_idx > last_shown_idx_of_the_list,
        # then (only by that argument) elements for next page should
        # be available (at least one)
        if [ "$from_what_idx_list_is_shown" -gt "$last_element" ]; then
            from_what_idx_list_is_shown=last_element
        fi
    fi
}

typeset -ga reply
reply=( -1 '' 1 1 )
integer from_what_idx_list_is_shown="$1"
integer current_idx="$2"
integer last_element="$3"

#
# Listening for input
#

case "$4" in
    (UP|k)
        # Are there any elements before the current one?
        [ "$current_idx" -gt 1 ] && current_idx=current_idx-1;
        # Previous page?
        _vlist_check_if_previous_page
        ;;
    (DOWN|j)
        # Are there any elements after the current one?
        [ "$current_idx" -lt "$last_element" ] && current_idx=current_idx+1;
        # Next page?
        _vlist_check_if_next_page
        ;;
    (PPAGE)
        current_idx=current_idx-page_height
        [ "$current_idx" -lt 1 ] && current_idx=1;
        _vlist_check_if_previous_page
        ;;
    (NPAGE|" ")
        current_idx=current_idx+page_height
        [ "$current_idx" -gt "$last_element" ] && current_idx=last_element;
        _vlist_check_if_next_page
        ;;
    (HOME|g)
        current_idx=1
        from_what_idx_list_is_shown=1
        ;;
    (END|G)
        current_idx=last_element
        from_what_idx_list_is_shown=last_element-page_height+1
        [ "$from_what_idx_list_is_shown" -lt 1 ] && from_what_idx_list_is_shown=1;
        ;;
    ($'\n')
        # Is that element selectable?
        if [[ ${VLIST_NONSELECTABLE_ELEMENTS[(r)$current_idx]} != $current_idx ]]; then
            # Save current element in the result variable
            reply=( $current_idx SELECT )
        fi
        ;;
    (q)
            reply=( -1 QUIT )
        ;;
    (*)
        ;;
esac

reply[3]="$current_idx"
reply[4]="$from_what_idx_list_is_shown"

# vim: set filetype=zsh:
