# Copy this file into /usr/share/zsh/[-version-]/functions
# and add 'autoload v-cd` to .zshrc
#
# This function allows to choose a directory from pushd stack
#
# Uses n-list

emulate -L zsh

zmodload zsh/curses
local IFS="
"

local list
local selected

VLIST_REMEMBER_STATE=0

list=( `dirs -p` )
list=( "${(@M)list:#*$1*}" )

[ "$#list" -eq 0 ] && echo "No matching directories"

[ -f ~/.config/zvt/v-cd.conf ] && . ~/.config/zvt/v-cd.conf
if [ "$#appended_dirs" -gt 1 ]; then
    typeset -a VLIST_NONSELECTABLE_ELEMENTS
    integer tmp_list_size="$#list"
    VLIST_NONSELECTABLE_ELEMENTS=( $(( tmp_list_size+1 )) $(( tmp_list_size+2 )) )
    list=( "$list[@]" "" "Hotlist:" "$appended_dirs[@]" )
else
    [ "$#list" -eq 0 ] && return 1
fi

n-list "${list[@]}"

if [ "$REPLY" -gt 0 ]; then
    selected="$list[$REPLY]"
    selected="${selected/#\~/$HOME}"
    echo "You have selected $selected"
    setopt NO_AUTO_PUSHD
    cd "$selected"
    setopt AUTO_PUSHD
fi

# vim: set filetype=zsh:
