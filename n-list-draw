# Copy this file into /usr/share/zsh/site-functions/
# and add 'autoload n-list-draw` to .zshrc

emulate -L zsh

zmodload zsh/curses

setopt localoptions typesetsilent extendedglob

_nlist_expand_tabs() {
    local chunk="$1"
    integer before_len="$2"
    REPLY=""

    while [ -n "$chunk" ]; do
        [[ "$chunk" = (#b)([^$'\t']#)$'\t'(*) ]] && {
            (( left_len=${#match[1]},
                tabs_count_all=(before_len+left_len)/8,
                all_text_len=(tabs_count_all+1)*8,
                chunk_padded_len=all_text_len-before_len ))

            REPLY+="${(r:$chunk_padded_len:: :)match[1]}"

            before_len+=chunk_padded_len
            chunk="$match[2]"
        } || {
            REPLY+="$chunk"
            break
        }
    done
}


_nlist_print_with_ansi() {
    local win="$1" text="$2" out col
    integer text_offset="$3" max_text_len="$4" text_len=0 no_match=0 nochunk_text_len to_skip_from_chunk to_chop_off_from_chunk

    # 1 - non-escaped text, 2 - first number in the escaped text, with ;
    # 3 - second number, 4 - text after whole escape text

    typeset -a c
    c=( black red green yellow blue magenta cyan white )

    while [[ -n "$text" && "$no_match" -eq 0 ]]; do
        if [[ "$text" = (#b)([^$'\x1b']#)$'\x1b'\[(#B)([0-9][0-9]\;|)(#b)([0-9][0-9]|)m(*) ]]; then
            # Text for further processing
            text="$match[3]"
            # Text chunk to output now
            out="$match[1]"
            # Save color
            col="$match[2]"
        else
            out="$text"
            no_match=1
        fi

        if [ -n "$out" ]; then
            _nlist_expand_tabs "$out" "$text_len"

            # Input text length without the current chunk
            nochunk_text_len=text_len
            # Input text length up to current chunk
            text_len+="$#REPLY"

            # Should start displaying with this chunk?
            # I.e. stop skipping left part of the input text?
            if (( text_len > text_offset )); then
                to_skip_from_chunk=text_offset-nochunk_text_len

                # LEFT - is chunk off the left skip boundary? +1 for 1-based index in string
                (( to_skip_from_chunk > 0 )) && REPLY="${REPLY[to_skip_from_chunk+1,-1]}"

                # RIGHT - is text off the screen?
                if (( text_len-text_offset > max_text_len )); then
                    to_chop_off_from_chunk=0+(text_len-text_offset)-max_text_len
                    REPLY="${REPLY[1,-to_chop_off_from_chunk-1]}"
                fi
                
                [ -n "$REPLY" ] && zcurses string "$win" "$REPLY"
            fi
        fi

        if (( no_match == 0 )); then
            if (( col >= 30 && col <= 37 )); then
                zcurses attr "$win" $c[col-29]/black
            elif [[ -n "$col" && "$col" -eq 0 ]]; then
                zcurses attr "$win" white/black
            fi
        fi
    done
}

integer highlight="$1"
integer page_height="$2"
integer page_width="$3"
local y_offset="$4"
local x_offset="$5"
local text_offset="$6"
local win="$7"
local ansi_mode="$8"
shift 8
integer max_text_len=page_width-x_offset

[ "$bold" = "0" ] && bold="" || bold="+bold"
[ "$ansi_mode" = "ansi" ] && zcurses attr "$win" $bold white/black

integer end_idx=1+page_height-1
[ "$end_idx" -gt "$#" ] && end_idx="$#"
integer y=y_offset

integer i text_len
local text
for (( i=1; i<=end_idx; i++ )); do
    zcurses move "$win" $y "$x_offset"

    [ "$i" = "$highlight" ] && zcurses attr "$win" +reverse
    if [ "$ansi_mode" != "ansi" ]; then
        # TODO horizontal scroll
        _nlist_expand_tabs "$@[i]"
        text="$REPLY"
        text_len="${#text}"

        [ "$text_len" -gt "$max_text_len" ] && text="${text[1,$max_text_len]}"
        zcurses string "$win" "$text"
    else
        _nlist_print_with_ansi "$win" "$@[i]" "$text_offset" "$max_text_len"
    fi
    [ "$i" = "$highlight" ] && zcurses attr "$win" -reverse

    y+=1
done

zcurses attr "$win" white/black
# vim: set filetype=zsh:
