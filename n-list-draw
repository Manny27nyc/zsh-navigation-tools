# Copy this file into /usr/share/zsh/site-functions/
# and add 'autoload n-list-draw` to .zshrc

emulate -L zsh

zmodload zsh/curses

setopt localoptions typesetsilent extendedglob

_nlist_expand_tabs() {
    local chunk="$1"
    integer before_len="$2"
    REPLY=""
    local tab=$'\t'

    while [ -n "$chunk" ]; do
        up_to_tab="${chunk%%$tab*}"
        if [ "$up_to_tab" != "$chunk" ]; then
            local remaining="${chunk[$#up_to_tab+2,-1]}"
            integer left_len="$#up_to_tab"
            integer tabs_count_all=$(( (before_len+left_len)/8 ))
            integer all_text_len=$(( (tabs_count_all+1)*8 ))
            integer chunk_padded_len=$(( all_text_len - before_len ))
            REPLY+="${(r:$chunk_padded_len:: :)up_to_tab}"

            before_len+=chunk_padded_len
            chunk="$remaining"
        else
            REPLY+="$chunk"
            break
        fi
    done
}


_nlist_print_with_ansi() {
    local win="$1"
    local text="$2"
    integer text_offset="$3"
    integer max_text_len="$4"
    integer text_len=0

    # 1 - non-escaped text, 2 - first number in the escaped text, with ;
    # 3 - second number, 4 - text after whole escape text

    local out
    integer no_match=0
    typeset -a c
    c=( black red green yellow blue magenta cyan white )

    while [[ -n "$text" && "$no_match" -eq 0 ]]; do
        if [[ "$text" = (#b)([^$'\x1b']#)$'\x1b'\[([0-9][0-9]\;|)([0-9][0-9]|)m(*) ]]; then
            # Text for further processing
            text="$match[4]"
            # Text chunk to output now
            out="$match[1]"
        else
            out="$text"
            no_match=1
        fi

        if [ -n "$out" ]; then
            _nlist_expand_tabs "$out" "$text_len"
            out="$REPLY"

            # Input text length without the current chunk
            integer nochunk_text_len=text_len
            # Input text length up to current chunk
            text_len+="$#out"

            # Should start displaying with this chunk?
            # I.e. stop skipping left part of the input text?
            if [ "$text_len" -gt "$text_offset" ]; then
                integer to_skip_from_chunk=text_offset-nochunk_text_len

                # LEFT - is chunk off the left skip boundary? +1 for 1-based index in string
                [ "$to_skip_from_chunk" -gt 0 ] && out="${out[to_skip_from_chunk+1,-1]}"

                integer remaining_text_len=text_len-text_offset
                integer remaining_nochunk_text_len=nochunk_text_len-text_offset

                # RIGHT - is text off the screen?
                if [ "$remaining_text_len" -gt "$max_text_len" ]; then
                    integer to_display_from_chunk=max_text_len-remaining_nochunk_text_len
                    [ "$to_display_from_chunk" -gt 0 ] && out="${out[1,to_display_from_chunk]}" || out=""
                fi
                
                [ -n "$out" ] && zcurses string "$win" "$out"
            fi
        fi

        if [ "$no_match" -eq 0 ]; then
            if [[ "$match[3]" -ge 30 && "$match[3]" -le 37 ]]; then
                zcurses attr "$win" $c[$(( match[3] - 29 ))]/black
            elif [[ -n "$match[3]" && "$match[3]" -eq 0 ]]; then
                zcurses attr "$win" white/black
            fi
        fi
    done
    zcurses attr "$win" white/black
}

integer highlight="$1"
integer start_idx="$2"
integer page_height="$3"
integer page_width="$4"
local y_offset="$5"
local x_offset="$6"
local text_offset="$7"
local win="$8"
local ansi_mode="$9"
shift 9
integer max_text_len=page_width-x_offset

[ -f ~/.config/znt/n-list.conf ] && . ~/.config/znt/n-list.conf
[ "$bold" = "0" ] && bold="" || bold="+bold"
[ "$ansi_mode" = "ansi" ] && zcurses attr "$win" $bold white/black

local -a list
list=( "$@" )

integer end_idx=start_idx+page_height-1
[ "$end_idx" -gt "$#list" ] && end_idx="$#list"
integer y=y_offset

integer i
for (( i=start_idx; i<=end_idx; i++ )); do
    zcurses move "$win" $y "$x_offset"

    local text="$list[$i]"

    [ "$i" = "$highlight" ] && zcurses attr "$win" +reverse
    if [ "$ansi_mode" != "ansi" ]; then
        # TODO horizontal scroll
        _nlist_expand_tabs "$list[$i]"
        text="$REPLY"
        integer text_len="${#text}"

        [ "$text_len" -gt "$max_text_len" ] && text="${text[1,$max_text_len]}"
        zcurses string "$win" "$text"
    else
        _nlist_print_with_ansi "$win" "$text" "$text_offset" "$max_text_len"
    fi
    [ "$i" = "$highlight" ] && zcurses attr "$win" -reverse

    y+=1
done

# vim: set filetype=zsh:
