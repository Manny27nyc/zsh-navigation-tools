# $1, $2, ... - elements of the list
# $VLIST_NONSELECTABLE_ELEMENTS - array of indexes (1-based) that cannot be selected
# $REPLY is the output variable - contains index (1-based) or -1 when no selection
#
# Copy this file into /usr/share/zsh/[-version-]/functions
# and add 'autoload n-list` to .zshrc
#
# This function outputs a list of elements that can be
# navigated with keyboard. Uses curses library

emulate -L zsh

zmodload zsh/curses

setopt localoptions typesetsilent localtraps extendedglob
trap "return" TERM INT QUIT
trap "_nlist_exit" EXIT

# Drawing and input
autoload n-list-draw n-list-input

# Cleanup before any exit
_nlist_exit() {
    zcurses 2>/dev/null delwin inner
    zcurses 2>/dev/null delwin main
    zcurses 2>/dev/null refresh
    zcurses end
}

# Outputs a message in the bottom of the screen
_nlist_status_msg() {
    # -1 for border, -1 for 0-based indexing
    zcurses move main $(( term_height - 1 - 1 )) 2
    zcurses clear main eol
    zcurses string main "$1"
}

#
# Main code
#

# Check if there is proper input
if [ "$#" -lt 1 ]; then
    echo "Usage: n-list element_1 ..."
    return 1
fi

integer term_height="$LINES"
integer term_width="$COLUMNS"
if [[ "$term_height" -lt 1 || "$term_width" -lt 1 ]]; then
    local stty_out=$( stty size )
    term_height="${stty_out% *}"
    term_width="${stty_out#* }"
fi
integer inner_height=term_height-3
integer inner_width=term_width-2
integer page_height=inner_height
integer page_width=inner_width

typeset -a list
integer last_element
local action
local final_key
integer selection

typeset -g REPLY
REPLY=-1

# Ability to remember the list between calls
if [[ -z "$VLIST_REMEMBER_STATE" || "$VLIST_REMEMBER_STATE" -eq 0 ]]; then
    VLIST_FROM_WHAT_IDX_LIST_IS_SHOWN=1
    VLIST_CURRENT_IDX=1
    VLIST_IS_SEARCH_MODE=0
    VLIST_SEARCH_BUFFER=""
fi

zcurses init
zcurses delwin main 2>/dev/null
zcurses delwin inner 2>/dev/null
zcurses addwin main "$term_height" "$term_width" 0 0
zcurses addwin inner "$inner_height" "$inner_width" 1 1

#
# Listening for input
#

local key keypad

# Clear input buffer
zcurses timeout main 0
zcurses input main key keypad
zcurses timeout main -1
key=""
keypad=""

while (( 1 )); do
    list=( "$@" )
    last_element="$#"

    # Do searching (filtering with string)
    if [ ! -z "$VLIST_SEARCH_BUFFER" ]; then
        # First remove non-selectable elements
        [ "$#VLIST_NONSELECTABLE_ELEMENTS" -gt 0 ] && for i in "${(nO)VLIST_NONSELECTABLE_ELEMENTS[@]}"; do
            list[$i]=()
        done

        # Next do the filtering
        list=( "${(@M)list:#(#i)*$VLIST_SEARCH_BUFFER*}" )
        last_element="$#list"
        [ "$VLIST_CURRENT_IDX" -gt "$last_element" ] && VLIST_CURRENT_IDX="$last_element"
        [ "$VLIST_CURRENT_IDX" -eq 0 ] && VLIST_CURRENT_IDX=1
        (( VLIST_FROM_WHAT_IDX_LIST_IS_SHOWN=0+((VLIST_CURRENT_IDX-1)/page_height)*page_height+1 ))
    fi

    # Output the list
    n-list-draw "$VLIST_CURRENT_IDX" "$VLIST_FROM_WHAT_IDX_LIST_IS_SHOWN" \
                "$page_height" "$page_width" 0 1 inner "$list[@]"

    zcurses border main
    if [ "$VLIST_IS_SEARCH_MODE" = "1" ]; then
        _nlist_status_msg "Filtering with: $VLIST_SEARCH_BUFFER"
    else
        _nlist_status_msg "Current #$VLIST_CURRENT_IDX (of #$last_element entries)"
    fi
    zcurses refresh main inner

    # Wait for input
    zcurses input main key keypad

    # Get the special (i.e. "keypad") key or regular key
    if [ ! -z "$key" ]; then
        final_key="$key" 
    elif [ ! -z "$keypad" ]; then
        final_key="$keypad"
    else
        _nlist_status_msg "Inproper input detected"
        zcurses refresh main inner
    fi

    n-list-input "$VLIST_CURRENT_IDX" "$VLIST_FROM_WHAT_IDX_LIST_IS_SHOWN" \
                    "$page_height" "$page_width" "$last_element" "$final_key" \
                    "$VLIST_IS_SEARCH_MODE" "$VLIST_SEARCH_BUFFER"

    selection="$reply[1]"
    action="$reply[2]"
    VLIST_CURRENT_IDX="$reply[3]"
    VLIST_FROM_WHAT_IDX_LIST_IS_SHOWN="$reply[4]"
    VLIST_IS_SEARCH_MODE="$reply[5]"
    VLIST_SEARCH_BUFFER="$reply[6]"

    if [ "$action" = "SELECT" ]; then
        REPLY="$selection"
        reply=( "$list[@]" )
        break
    elif [ "$action" = "QUIT" ]; then
        break
    fi
done

# vim: set filetype=zsh:
